pkgname=lfm-git
_pkgname=lfm
pkgver=r612.d8387cf
pkgrel=1
pkgdesc="terminal file manager"
url="https://github.com/kmarius/lfm"
license=('MIT')
arch=('x86_64' 'aarch64')
depends=('luajit' 'libev' 'lua51-posix')
makedepends=('git' 'cmake' 'gcc')
optdepends=("trash-cli: trash files")
source=(git+https://github.com/kmarius/lfm)
conflicts=()
md5sums=('SKIP')
options=('!strip')

pkgver() {
	cd "${_pkgname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${_pkgname}"
	cmake -Bbuild -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=RelWithDebInfo
	cmake --build build
}

package() {
	cd "${_pkgname}"
	DESTDIR="${pkgdir}" cmake --install build
}
